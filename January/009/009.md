# Day 9: JavaScript Archives 

JavaScript is the language of choice for multiple malware distributors. Often times the JavaScript malware is packaged in a zip archive themed as a software update or social engineering ploys. Todays yara rule is detecting zip files with a compressed javascript file. 

The .zip malware sample I reviewed while writing this rule was observed earlier this week distributing NetSupport as a backdoor: https://infosec.exchange/@malware_traffic/109717283314388425.

# Yara Rule

Here's the Yara rule that I created for malware family Z:

```
rule small_zip_js {
  meta:
    author = "Colin Cowie"
    description = "Attempts to find .zip files containing a JS file"
    reference = "https://kevinwinata.com/blog/zip-header-yara/"
  strings:
    $zip_header = {50 4B 03 04}
    $js = ".js"
    $json = ".json"
    $exe = ".exe"
    $msi = ".msi"
    $dll = ".dll"
    $iso = ".iso"
    $vbs = ".vbs"
    $ps1 = ".ps1"
    $php = ".php"
    $css = ".css"
    $manifest = ".manifest"
    $rdf = ".rdf"
    $xul = ".xul"
  condition:
    filesize<10KB and
    not ($json or $exe or $msi or $dll or $iso or $vbs or $ps1 or $php or $css or $manifest or $rdf or $xul) and
    for any i in (1..#zip_header):
      ($js in (@zip_header[i]+30..@zip_header[i]+30+uint16(@zip_header[i]+26)))
}
```

# Results

At first I didn't filter out `.css`, `.json` or `manifest`. My results included a lot of browser extension archives that were not inhertintlly suspicious. After adding in the extra conditonals the rule returned slightly better results. There's proably a lot better way to perform these checks but for now this is what I will use.

Overall this rule isn't super great but it was fun to experiment with!

# References
- https://infosec.exchange/@malware_traffic/109717283314388425
- https://kevinwinata.com/blog/zip-header-yara/