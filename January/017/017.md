# Day 17 - Gootloader Archive Detection

Gootloader uses search engine optimization poisoning to distribute malware in the form of archives with javascript files. The file names are typically things a user searches for like `what_is_a_mortgage_variation_agreement 74475 .js` or `royal_canadian_mint_collective_agreement_2018 14523 .js`.

Todays yara rule looks for Gootloader archives by checking for a single javascript file that includes underscores in its name.

# Yara Rule

Here is the Yara rule that I created for detecting gootloader archives
```
rule mal_zip_gootloader {
    meta:
  	    author = "Colin Cowie"
        description = "Detects gootloader archives"
        references = "https://www.mandiant.com/resources/blog/tracking-evolution-gootloader-operations"
  strings:
        $js_filename = {5f [0-64] 5f [0-64] 5f [0-64] 2e 6a 73} // _[0-64]_[0-64]_[0-64].js
        $json = ".json"
        $js = ".js"
        $manifest = ".manifest"
        $html = ".html"
        $css = ".css"
        $php = ".php"
    condition:
        (uint32(0) == 0x04034b50) // check for zip file header
        and filesize<150KB
        and $js_filename
        and #js<3 // check for multiple js files
        and not ($json or $css or $html or $manifest or $php) // check for other files commonly found with js scripts
}

```
# Results
While testing out this rule I added in the json exclusion, filesize limits and ".js" count. All of which helped improve the accuracy of this rule a lot! I noticed a false positive with a .jsp file so this rule is not perfect but did a nice job at picking up gootloader samples on VirusTotal. Todays rule was a good continuation of practicing with Yara jumps. 

# References
- https://www.mandiant.com/resources/blog/tracking-evolution-gootloader-operations
- https://news.sophos.com/en-us/2021/03/01/gootloader-expands-its-payload-delivery-options/